% we collect in this file the main Theorems that appear in the paper
paper_theorems: THEORY
BEGIN
	IMPORTING unification_alg


Delta, Nabla, Nabla_, G, G1, Gamma, Gamma1: VAR fresh_context
pi: VAR perm

a: VAR atom
X: VAR variable
t, s: VAR term
sigma, sigma1, delta, gamma, gamma_: VAR sub
vars2avoid, fixed_vars, V, PV: VAR finite_set[variable]

up, unif_pair: VAR unif_pair
unif_prb, P, P1: VAR unif_prb
unif_prb1: VAR unif_prb
id: VAR sub
input, input_: VAR nice_input
rho: VAR renaming

% function "is_sol?" assumes that the set of variables is $\mathbb{X}$. 
% soundness for nice inputs.
unify_alg_sound: THEOREM 
   LET (Gamma, P, sigma, V, PV) = input IN 
   is_sol?(Delta, delta)(Gamma1, null, sigma1, PV) AND 
   member((Gamma1, sigma1), unify(input)) IMPLIES
   is_sol?(Delta, delta)(Gamma, P, sigma, PV)

% we put in the hypothesis that $t$ and $s$ are well-formed
% soundness for matching
unify_alg_sound_match: COROLLARY  
   is_instance?(Delta, delta)(Gamma, sigma, vars(s)) AND well_formed?((t, s)) AND
   member((Gamma, sigma), unify(null, cons((t, s), null), null, vars(t, s), vars(s))) IMPLIES 
   is_sol?(Delta, delta)(null, cons((t, s), null), null, vars(s)) 

% soundness for nominal AC-equality checking
unify_alg_sound_equality_check: COROLLARY 
   is_instance?(Delta, delta)(Gamma, sigma, vars(t, s)) AND well_formed?((t, s)) AND
   member((Gamma, sigma), unify(null, cons((t, s), null), null, vars(t, s), vars(t, s))) IMPLIES 
   is_sol?(Delta, delta)(null, cons((t, s), null), null, vars(t, s)) 

% function "is_sol?" assumes that the set of variables is $\mathbb{X}$. 
% completeness for nice inputs
unify_alg_complete: THEOREM   
   LET (Gamma, P, sigma, V, PV) = input IN 
   is_sol?(Delta, delta)(Gamma, P, sigma, PV) AND contained?(delta, V)
   AND subset?(vars(Delta), V) IMPLIES 
   EXISTS Gamma1, sigma1: 
      member((Gamma1, sigma1), unify(input)) AND
      is_instance?(Delta, delta)(Gamma1, sigma1, V, PV)

% completeness for matching
unify_alg_complete_match: COROLLARY 
   is_sol?(Delta, delta)(null, cons((t, s), null), null, vars(s))
   AND contained?(delta, V) AND subset?(vars(Delta), V) AND subset?(vars(t, s), V) AND
   well_formed?((t, s)) IMPLIES 
   EXISTS Gamma1, sigma1: 
      member((Gamma1, sigma1), unify(null, cons((t, s), null), null, V, vars(s))) AND
      is_instance?(Delta, delta)(Gamma1, sigma1, V, vars(s))

% completeness for AC-equality check 
unify_alg_complete_equality_check: COROLLARY 
   is_sol?(Delta, delta)(null, cons((t, s), null), null, vars(t, s))
   AND contained?(delta, V) AND subset?(vars(Delta), V) AND subset?(vars(t, s), V) AND
   well_formed?((t, s)) IMPLIES 
   EXISTS Gamma1, sigma1: 
      member((Gamma1, sigma1), unify(null, cons((t, s), null), null, V, vars(t, s))) AND
      is_instance?(Delta, delta)(Gamma1, sigma1, V, vars(t, s))

END paper_theorems 
